plugins {
    id 'net.neoforged.gradleutils'
    id 'java-library'
    id 'maven-publish'
}

group = 'net.neoforged'
version = gradleutils.getTagOffsetVersion()
println('Version: ' + version)

changelog {
    fromTag '0.3'
}
changelog.disableAutomaticPublicationRegistration()

subprojects { subproject ->

    subproject.apply plugin: 'java-library'
    subproject.apply plugin: 'maven-publish'
    subproject.apply plugin: 'signing'

    subproject.group = 'net.neoforged'
    subproject.version = rootProject.version

    sourceSets {
        test {
            java {
                srcDir file('src/binks/java')
            }
            resources.srcDir file('src/binks/resources')
        }
    }

    repositories {
        mavenCentral()
        maven {
            name 'Forge'
            url 'https://maven.neoforged.net/'
        }
    }

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
        withSourcesJar()
    }

    project.tasks.withType(JavaCompile).all {
        JavaToolchainService service = getProject().getExtensions().getByType(JavaToolchainService.class);
        Provider<JavaCompiler> compiler = service.compilerFor(s -> s.getLanguageVersion().set(JavaLanguageVersion.of(8)));

        it.javaCompiler = compiler;
    }

    dependencies {
        testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: "${junitVersion}"
        testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: "${junitVersion}"
    }

    test {
        useJUnitPlatform()
    }

    processTestResources {
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    }

    jar {
        manifest {
            attributes([
                    'Maven-Artifact'          : "${project.group}:${project.archivesBaseName}:${project.version}",
                    "Specification-Title"     : project.name,
                    "Specification-Vendor"    : "NeoForged",
                    "Specification-Version"   : "1", // We are version 1 of ourselves
                    "Implementation-Title"    : project.name,
                    "Implementation-Version"  : "${project.version}",
                    "Implementation-Vendor"   : "NeoForged",
                    "Automatic-Module-Name"   : project.name,
            ])
        }
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java

                pom {
                    name = project.name
                    description = 'Jar in Jar Utilities including the required NIO FileSystems. SubSystem: ' + project.name
                    url = 'https://github.com/NeoForged/JarJar'

                    scm {
                        url = 'https://github.com/NeoForged/JarJar'
                        connection = 'scm:git:git://github.com/NeoForged/JarJar.git'
                        developerConnection = 'scm:git:git@github.com:NeoForged/JarJar.git'
                    }

                    issueManagement {
                        system = 'github'
                        url = 'https://github.com/NeoForged/JarJar/issues'
                    }

                    licenses {
                        license {
                            name = 'LGPL 2.1'
                            url = 'https://github.com/NeoForged/JarJar/blob/main/LICENSE'
                            distribution = 'repo'
                        }
                    }

                    developers {
                        developer {
                            id = 'neoforged'
                            name = 'NeoForged'
                            email = 'contact@neoforged.net'
                            url = 'https://github.com/NeoForged/'
                        }
                    }
                }
            }
        }
        repositories {
            maven gradleutils.getPublishingForgeMaven()
        }
    }

    if (System.getenv('GPG_PRIVATE_KEY')) {
        signing {
            final signingKey = System.getenv('GPG_PRIVATE_KEY') ?: ''
            final signingPassword = System.getenv('GPG_KEY_PASSWORD') ?: ''
            useInMemoryPgpKeys(signingKey, signingPassword)
            sign publishing.publications.mavenJava
        }
    }
}