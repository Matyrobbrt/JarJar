plugins {
    id 'net.neoforged.gradleutils'
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

group = 'net.neoforged'
version = gradleutils.version
println('Version: ' + version)

changelog {
    from '0.3'
    disableAutomaticPublicationRegistration()
}

subprojects { Project subproject ->

    subproject.apply plugin: 'java-library'
    subproject.apply plugin: 'maven-publish'

    subproject.group = 'net.neoforged'
    subproject.version = rootProject.version

    gradleutils {
        setupSigning(project: subproject, signAllPublications: true)
    }

    sourceSets {
        test {
            java {
                srcDir file('src/binks/java')
            }
            resources.srcDir file('src/binks/resources')
        }
    }

    repositories {
        mavenCentral()
        maven {
            name 'Forge'
            url 'https://maven.neoforged.net/'
        }
    }

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(8)
        }
        withSourcesJar()
    }

    dependencies {
        testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: "${junitVersion}"
        testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: "${junitVersion}"
    }

    test {
        useJUnitPlatform()
    }

    processTestResources {
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    }

    jar {
        manifest {
            attributes([
                    'Maven-Artifact'          : "${project.group}:${project.archivesBaseName}:${project.version}",
                    "Specification-Title"     : project.name,
                    "Specification-Vendor"    : "NeoForged",
                    "Specification-Version"   : "1", // We are version 1 of ourselves
                    "Implementation-Title"    : project.name,
                    "Implementation-Version"  : "${project.version}",
                    "Implementation-Vendor"   : "NeoForged",
                    "Automatic-Module-Name"   : project.name,
            ])
        }
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java

                pom {
                    name = project.name
                    description = 'Jar in Jar Utilities including the required NIO FileSystems. SubSystem: ' + project.name
                    url = 'https://github.com/NeoForged/JarJar'

                    scm {
                        url = 'https://github.com/NeoForged/JarJar'
                        connection = 'scm:git:git://github.com/NeoForged/JarJar.git'
                        developerConnection = 'scm:git:git@github.com:NeoForged/JarJar.git'
                    }

                    issueManagement {
                        system = 'github'
                        url = 'https://github.com/NeoForged/JarJar/issues'
                    }

                    licenses {
                        license {
                            name = 'LGPL 2.1'
                            url = 'https://github.com/NeoForged/JarJar/blob/main/LICENSE'
                            distribution = 'repo'
                        }
                    }

                    developers {
                        developer {
                            id = 'neoforged'
                            name = 'NeoForged'
                            email = 'contact@neoforged.net'
                            url = 'https://github.com/NeoForged/'
                        }
                    }
                }
            }
        }
        repositories {
            maven gradleutils.publishingMaven
        }
    }
}